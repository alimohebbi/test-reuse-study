apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'jacoco'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "privacyfriendlyshoppinglist.secuso.org.privacyfriendlyshoppinglist"
        minSdkVersion 17
        targetSdkVersion 22
        versionCode 6
        versionName "1.0.8"
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }

    testOptions {
        unitTests {
            returnDefaultValues = true
        }
    }

    sourceSets {
        main {
            res.srcDirs =
                    [
                            'src/main/res/layouts/about',
                            'src/main/res/layouts/baseactivity',
                            'src/main/res/layouts/help',
                            'src/main/res/layouts/main',
                            'src/main/res/layouts/preferences',
                            'src/main/res/layouts/products',
                            'src/main/res/layouts/settings',
                            'src/main/res/layouts/deletelists',
                            'src/main/res/layouts/deleteproducts',
                            'src/main/res/layouts/shared',
                            'src/main/res/layouts/shoppinglist',
                            'src/main/res/layouts/statistics',
                            'src/main/res/layouts/camera',
                            'src/main/res/layouts/tutorials',
                            'src/main/res'
                    ]
        }
    }
}

jacoco { toolVersion = "0.7.5+" }

task jacocoTestReport(type: JacocoReport) {
    def coverageSourceDirs = [
            '../app/src/main/java'
    ]
    group = "Reporting"
    description = "Generate Jacoco coverage reports after running tests."
    reports {
        xml.enabled = true
        html.enabled = true
    }
    classDirectories = fileTree(
            dir: './build/intermediates/classes/debug',
            excludes: ['**/R*.class',
                       '**/*$InjectAdapter.class',
                       '**/*$ModuleAdapter.class',
                       '**/*$ViewInjector*.class'
            ])
    sourceDirectories = files(coverageSourceDirs)
    executionData = files("$buildDir/outputs/code-coverage/connected/coverage.exec")
    doFirst {
        new File("$buildDir/intermediates/classes/").eachFileRecurse { file ->
            if (file.name.contains('$$')) {
                file.renameTo(file.path.replace('$$', '$'))
            }
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    // android
    androidTestCompile 'com.android.support:support-annotations:25.3.1'
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.android.support:recyclerview-v7:25.3.1'
    compile 'com.android.support:cardview-v7:25.3.1'
    // tests
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.9.5'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    testCompile 'pl.pragmatists:JUnitParams:0.3.6'
    // dependency injection
    compile 'com.squareup.dagger:dagger:1.2.5'
    provided 'com.squareup.dagger:dagger-compiler:1.2.5'
    // persistence
    compile 'com.j256.ormlite:ormlite-android:4.46'
    // reactive x
    compile 'io.reactivex:rxandroid:1.2.0'
    compile 'io.reactivex:rxjava:1.1.5'
    // joda
    compile 'joda-time:joda-time:2.2'
    // graphics
    compile 'com.github.PhilJay:MPAndroidChart:v3.0.0-beta1'
    // image zoom
    compile 'com.davemorrissey.labs:subsampling-scale-image-view:3.5.0'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile 'com.android.support.test.espresso:espresso-contrib:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }
}
